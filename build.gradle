plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

version = project.version;

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.petrus.dev/public" }
    maven {
        url = 'https://repo.alessiodp.com/releases/'//For the custom libby-core
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs')

    implementation("net.kyori:adventure-api:4.26.1")
    implementation("net.kyori:adventure-text-serializer-gson:4.26.1")
    implementation("net.kyori:adventure-text-serializer-legacy:4.26.1")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly(files(System.getProperty("user.home") + "/AppData/Roaming/Hytale/install/release/package/game/$game_build/Server/HytaleServer.jar"))

    implementation 'net.byteflux:libby-core:1.2.0'
    compileOnly 'com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4'

    compileOnly "jakarta.annotation:jakarta.annotation-api:3.0.0"
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.apache.commons:commons-lang3:3.15.0'
    implementation 'commons-io:commons-io:2.21.0'
    //Logging
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveClassifier.set('')
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

processResources {
    var expandProps = [
            'name'           : project.name,
            'version'        : project.version,
            'group'          : project.group,
            'author'         : author,
            'mod_description': mod_description,
            'website'        : website,
            'server_version' : server_version,
            'entry_point'    : entry_point
    ]

    filesMatching(['manifest.json']) {
        expand expandProps
    }

    inputs.properties(expandProps)
}

def serverRunDir = file("$projectDir/run")

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=$hytaleHome/$patchline/package/game/latest/Assets.zip --mods=${file("src/main/").absolutePath}  --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}


publishing {
    repositories {
        maven {
            name = "PetrusRepo"
            url = "https://maven.petrus.dev/public"
            credentials {
                username = System.env.PETRUSMAVEN_ACTOR
                password = System.env.PETRUSMAVEN_TOKEN
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        petrus(MavenPublication) {
            groupId = project.group
            from(components.java)
            pom.withXml {
                asNode().appendNode('url','https://petrus.dev')
            }
        }
    }
}